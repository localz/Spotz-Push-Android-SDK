apply plugin: 'com.android.application'

android {
    buildToolsVersion "26.0.1"
    compileSdkVersion 26

    defaultConfig {
        applicationId "com.sample.spotzpush"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 17
        versionName "2.2.0"

        buildConfigField 'String', 'SPOTZ_PUSH_PROJECT_ID', '"Insert Spotz Push Project ID here"'
        buildConfigField 'String', 'SPOTZ_PUSH_PROJECT_KEY', '"Insert Spotz Push Android Client key here"'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        fcm {
            applicationId 'com.sample.spotzpush.fcm'
            versionName + '-fcm'
        }
        gcm {
            applicationId 'com.sample.spotzpush.gcm'
            versionName + '-gcm'
            buildConfigField 'String', 'PLAY_PROJECT_ID', '"<Insert Google Project Number here>"'
        }
        socket {
            applicationId 'com.sample.spotzpush.socket'
            versionName + '-socket'
        }
    }

    sourceSets {
        fcm {
            manifest.srcFile 'src/fcm/AndroidManifest.xml'
            java.srcDirs = ['src/fcm/java']
            res.srcDirs = ['src/fcm/res']
            assets.srcDirs = ['src/fcm/assets']
        }
        gcm {
            manifest.srcFile 'src/gcm/AndroidManifest.xml'
            java.srcDirs = ['src/gcm/java']
            res.srcDirs = ['src/gcm/res']
            assets.srcDirs = ['src/gcm/assets']
        }
        socket {
            manifest.srcFile 'src/socket/AndroidManifest.xml'
            java.srcDirs = ['src/socket/java']
            res.srcDirs = ['src/socket/res']
            assets.srcDirs = ['src/socket/assets']
        }
    }

    ext {
        supportLibVersion = "26.0.0"
        playServicesVersion = "11.2.0"
        spotzPushSdkVersion = "2.1.0"
    }

    dependencies {
        compile fileTree(dir: 'libs', include: ['*.jar'])

        //Required for sample app only
        compile "com.android.support:appcompat-v7:$supportLibVersion"

        //Spotz Push SDK dependencies start
        compile 'com.google.code.gson:gson:2.8.1'
        compile('com.google.http-client:google-http-client:1.21.0') {
            exclude module: 'httpclient'
        }
        compile('com.google.http-client:google-http-client-gson:1.21.0') {
            exclude module: 'httpclient'
        }

        compile "com.android.support:support-v4:$supportLibVersion"
        compile "com.google.android.gms:play-services-base:$playServicesVersion"

        //Only required for FCM
        fcmCompile "com.google.firebase:firebase-messaging:$playServicesVersion"
        fcmCompile "com.google.android.gms:play-services-location:$playServicesVersion"
        fcmCompile "com.localz.spotzpush.sdk:spotz-push-sdk-fcm:$spotzPushSdkVersion@aar"
        //Only required for GCM
        gcmCompile "com.google.android.gms:play-services-gcm:$playServicesVersion"
        gcmCompile "com.google.android.gms:play-services-location:$playServicesVersion"
        gcmCompile "com.localz.spotzpush.sdk:spotz-push-sdk-gcm:$spotzPushSdkVersion@aar"

        //Only required for socket.io
        socketCompile "com.localz.spotzpush.sdk:spotz-push-sdk-socket:$spotzPushSdkVersion@aar"
        socketCompile ('io.socket:socket.io-client:0.8.3') {
            // excluding org.json which is provided by Android
            exclude group: 'org.json', module: 'json'
        }
    }
}

// socket doesn't need google-services
if (!getGradle().getStartParameter().getTaskRequests()
        .toString().contains("Socket")){
    apply plugin: 'com.google.gms.google-services'
}
